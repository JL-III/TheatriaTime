plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
    id 'maven-publish'
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.j256.ormlite', 'com.playtheatria.theatriatime.shaded.ormlite'
    relocate 'com.playtheatria.jl-iii.generalutils', 'com.playtheatria.jl-iii.shaded.generalutils'
}

tasks.build {
    dependsOn shadowJar
    dependsOn tasks.processResources
}

tasks.publish {
    dependsOn tasks.build
}

group = 'com.playtheatria'
version = 'v0.0.1-alpha.6'
ext.generalUtilsVersion = 'v2.0.0-alpha.10'

tasks.processResources {
    filesMatching("plugin.yml") {
        expand(
                version: project.version,
                generalUtilsVersion: generalUtilsVersion
        )
    }
}

publishing {
    publications {
        create("gpr", MavenPublication) {
            groupId = 'com.playtheatria'
            artifactId = 'theatriatime'  // Ensure lowercase and correct naming
            version = project.version

            from components.java  // Ensures proper packaging
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/JL-III/TheatriaTime"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/JL-III/GeneralUtils"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    implementation "com.playtheatria.jl-iii:generalutils:${generalUtilsVersion}"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
